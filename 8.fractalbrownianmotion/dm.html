<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <title>Shader codes in HTML waow! :O</title>
    <style>
      body { margin: 0; }
    </style>
  </head>
  <body>
    <div id="links-to-others"></div>
    <div id="container"></div>
    <script id="vertexShader" type="x-shader/x-vertex">
      varying vec2 vUv;
      
      void main() {
	  vUv = uv;
	  gl_Position = projectionMatrix * viewMatrix * modelMatrix * vec4(position, 1.0);
      }
    </script>
    <script id="fragmentShader" type="x-shader/x-fragment">
      #define PI 3.14159265359

      varying vec2 vUv;
      uniform float u_time;

      float random(in vec2 st){
	  return fract(sin(dot(st.xy, vec2(12.85722, 78.233))) * 4738.523481);
      }

      float noise(in vec2 st){
	  vec2 i = floor(st);
	  vec2 f = fract(st);

	  //four corners of 2D tile
	  float a = random(i);
	  float b = random(i + vec2(1.0, 0.0));
	  float c = random(i + vec2(0.0, 1.0));
	  float d = random(i + vec2(1.0, 1.0));

	  vec2 u = f * f * (3.0 - 2.0 * f);

	  return mix(a, b , u.x) +
	      (c - a) * u.y * (1.0 - u.x) +
	      (d - b) * u.x * u.y;
      }

      // FRACTAL BROWNIAN MOTION SEASON 2
#define OCTAVES 5
      float fbm(in vec2 st){
	  float value = 0.0;
	  float amplitude = 0.5;
	  vec2 shift = vec2(100.0);

	  // Rotate to reduce axial bias
	  mat2 rot = mat2(cos(0.5), sin(0.5),
			  -sin(0.5), cos(0.5));

	  for(int i = 0; i < OCTAVES; i++){
	      value += amplitude * noise(st);
	      st = rot * st * 2.0 + shift;
	      amplitude *= 0.5;
	  }

	  return value;
      }

      void main() {
	  vec2 st = vUv;

	  st *= 5.0;

	  vec3 color = vec3(0.0);

	  vec2 q = vec2(0.);
	  q.x = fbm( st );
	  q.y = fbm( st + vec2(1.0));

	  vec2 r = vec2(0.);
	  r.x = fbm( st + 1.0*q + vec2(1.7,9.2)+ 0.15*u_time );
	  r.y = fbm( st + 1.0*q + vec2(8.3,2.8)+ 0.126*u_time);
	  
	  float f = fbm( st + r);

	  // not sure if these numbers are predetermined
	  color = mix(vec3(0.101961,0.619608,0.666667),
		      vec3(0.666667,0.666667,0.498039),
		      clamp((f*f)*4.0,0.0,1.0));
	  
	  color = mix(color,
		      vec3(0,0,0.164706),
		      clamp(length(q),0.0,1.0));

	  color = mix(color,
		      vec3(0.666667,1,1),
		      clamp(length(r.x),0.0,1.0));	  
	  
	  gl_FragColor = vec4((f*f*f+.6*f*f+.5*f)*color,1.);
	  
      }
    </script>
    <script type="module" src="/main.js"></script>
  </body>
</html>
